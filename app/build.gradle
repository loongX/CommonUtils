apply plugin: 'com.android.application'


ext {
    versionCode = 1
    versionName = '1.0.0'
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.star.commonutils"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode project.property("versionCode")
        versionName project.property("versionName")

        multiDexEnabled true
    }
    //配置签名
    signingConfigs {
        release {
            storeFile file('../common_utils.jks')
            storePassword "${JKS_PASSWORD}"
            keyAlias "${JKS_THEME}"
            keyPassword "${JKS_PASSWORD}"
        }
    }
    //配置编译类型
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            buildConfigField 'boolean', 'BUILD_DEBUG', 'true'  //添加BuildConfig配置
        }
        release {
            minifyEnabled true
            shrinkResources true
            buildConfigField 'boolean', 'BUILD_DEBUG', 'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        buildTypes.all {
            type ->
                manifestPlaceholders = [VERSION_TYPE_VALUE: name]
                signingConfig signingConfigs.release
        }
    }

    //配置渠道
    flavorDimensions 'default'
    productFlavors {
        xiaomi {}
        baidu {}
        productFlavors.all { flavor ->
            dimension 'default'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            //重命名apk文件名称
            outputFileName = "CommonUtils" +
                    "-${variant.productFlavors[0].name}-${variant.buildType.name}" +
                    "-${variant.versionName}(${variant.versionCode}).apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'
}
